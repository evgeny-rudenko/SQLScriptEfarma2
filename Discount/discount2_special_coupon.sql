USE [Farma]
GO
/****** Object:  StoredProcedure [dbo].[DISCOUNT2_SPECIAL_COUPON]    Script Date: 07/16/2017 19:50:02 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[DISCOUNT2_SPECIAL_COUPON](
    @ID_DISCOUNT2_CARD_GLOBAL UNIQUEIDENTIFIER
)
AS
    DECLARE @PARAMS VARCHAR(4000)
    SELECT TOP 1
        @PARAMS = SPECIAL_PARAMS
    FROM #SPECIAL_PARAMS

    DECLARE @HDOC INT
    DECLARE @VALUE INT
    EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @PARAMS
    SELECT
        @VALUE = VALUE
    FROM OPENXML(@HDOC, '/XML')WITH(
        VALUE INT 'VALUE'
    )
    EXEC SP_XML_REMOVEDOCUMENT @HDOC
    
    DECLARE @SUM NUMERIC(18,10)
    SELECT
        @SUM = SUM(ISNULL(SUM_WITH_DISCOUNT,0))
    FROM #CHEQUEINFO

    DECLARE @MAX INT
    SET @MAX = CONVERT(INT, FLOOR((@SUM * 0.1) / 100.0000))

    DECLARE @COUPON MONEY
    SET @COUPON = 100.0000 * CASE WHEN @MAX<=ISNULL(@VALUE,0) THEN @MAX ELSE ISNULL(@VALUE,0) END

    IF (@COUPON=0) RETURN

    DECLARE CUR CURSOR LOCAL FOR
    SELECT
        ID_LOT_GLOBAL,
        RATING = CONVERT(NUMERIC(18,10), SUM_WITH_DISCOUNT) / @SUM
    FROM #CHEQUEINFO

    DECLARE @ID_LOT_GLOBAL UNIQUEIDENTIFIER
    DECLARE @RATING NUMERIC(18,10)

    DECLARE @IS_LAST BIT
    DECLARE @COUNTER INT
    SET @COUNTER=0
    DECLARE @REST MONEY
    SET @REST = @COUPON

    OPEN CUR
    WHILE 1=1 BEGIN
        FETCH NEXT FROM CUR INTO @ID_LOT_GLOBAL, @RATING
        IF (@@FETCH_STATUS<>0) BREAK            

        SET @COUNTER=@COUNTER+1

        SET @IS_LAST = 0
        SELECT @IS_LAST = 1
        FROM #CHEQUEINFO
        HAVING COUNT(*)=@COUNTER

        DECLARE @DISCOUNT_VALUE MONEY

        SET @DISCOUNT_VALUE = CASE WHEN ROUND(@RATING * @COUPON,2)>@REST OR ISNULL(@IS_LAST,0) = 1 THEN @REST ELSE ROUND(@RATING * @COUPON,2) END

        UPDATE C SET
            DISCOUNT_VALUE = ISNULL(DISCOUNT_VALUE, 0) + @DISCOUNT_VALUE,
            SUM_WITH_DISCOUNT = SUM_WITH_DISCOUNT - ISNULL(@DISCOUNT_VALUE,0)
        FROM #CHEQUEINFO C
        WHERE ID_LOT_GLOBAL = @ID_LOT_GLOBAL

        
        SET @REST = @REST - @DISCOUNT_VALUE
        IF (@REST=0)
            BREAK
    END
    CLOSE CUR
    DEALLOCATE CUR    
RETURN